buildscript {
    repositories {
        jcenter()
        maven { name = 'forge'; url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'maven'

archivesBaseName = name
version project.version + '-' + getBuildNumber()
group project.groupId

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2705"
	mappings = "snapshot_20171003"
    makeObfSourceJar = false
    runDir = "run"
    replace "\${version}", project.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
	
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

repositories {
    jcenter()

    maven {
        name = 'Mobius Repo'
        url = 'http://mobiusstrip.eu/maven'
    }
    maven {
        name = 'CB Repo'
        url = 'http://chickenbones.net/maven/'
    }
    maven {
        name = 'tehnut'
        url = 'http://tehnut.info/maven/'
    }
    maven {
        name = 'amadornes'
        url = 'http://maven.amadornes.com/'
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name = 'Tesla Api'
        url 'http://maven.epoxide.xyz'
    }
    maven {
        name = "Team Acronym Coders"
        url "http://maven.blamejared.com"
    }
    mavenCentral()
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // JEI
//    compile "mezz.jei:jei_1.12.2:4.11.0.+"

    // Craft Tweaker
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.9.+"
    deobfCompile "CraftTweaker2:CraftTweaker2-API:4.1.9.+"
    deobfCompile "CraftTweaker2:ZenScript:4.1.9.+"
    deobfCompile "com.blamejared:MTLib:3.0.4.+"
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.2.+"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.10.1.+"

    // Forgelin and ModelLoader
	compile files("lib/modelloader-1.0.5.jar", "lib/Forgelin-1.6.0.jar")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    if(buildNumber == null || buildNumber == ""){
        buildNumber = "dev"
    }
    return buildNumber
}