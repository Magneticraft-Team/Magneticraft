buildscript {
    ext.project_kotlin_version = '1.2.50'
    ext.mod_version = '2.8.2'
    
    repositories {
        jcenter()
        maven { name = 'forge'; url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project_kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'maven'

archivesBaseName = name
version mod_version + '-' + getBuildNumber()
group 'com.cout970.magneticraft'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.12.2-14.23.5.2768"
	mappings = "snapshot_20171003"
    makeObfSourceJar = false
    runDir = "run"

//    replaceIn 'Magneticraft.kt'
//    replace '@VERSION@', mod_version
}

repositories {
    jcenter()

    maven {
        name = 'Mobius Repo'
        url = 'http://mobiusstrip.eu/maven'
    }
    maven {
        name = 'CB Repo'
        url = 'http://chickenbones.net/maven/'
    }
    maven {
        name = 'tehnut'
        url = 'http://tehnut.info/maven/'
    }
    maven {
        name = 'amadornes'
        url = 'http://maven.amadornes.com/'
    }
    maven {
        name = 'Progwml6 maven'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name = 'TCL'
        url 'http://maven.mcmoddev.com'
    }
    maven {
        name = "Team Acronym Coders"
        url "http://maven.blamejared.com"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "Industrial foregoing"
        url = "http://dyonovan.com/maven2"
    }
    maven {
        url 'http://maven.covers1624.net/'
    }
    maven { name = 'forgelin'; url = 'http://maven.shadowfacts.net' }
    mavenCentral()
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$project_kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$project_kotlin_version"
    compile "net.shadowfacts:Forgelin:1.8.3"

    // JEI
//    compile "mezz.jei:jei_1.12.2:4.11.0.+" // commented causes gradle to include jei twice

    // Craft Tweaker
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.9.+"
    compile "CraftTweaker2:CraftTweaker2-API:4.1.9.+"
    compile "CraftTweaker2:ZenScript:4.1.9.+"
    compile "com.blamejared:MTLib:3.0.5.+"
    // Tinkers
    compile "slimeknights.mantle:Mantle:1.12-1.3.2.+"
    compile "slimeknights:TConstruct:1.12.2-2.10.1.+"
    // Buildcraft
    compile "com.mod-buildcraft:buildcraft:7.99.12"
    
//    compileOnly "com.buuz135.industrial.IndustrialForegoing:industrialforegoing:1.12.2-1.11.2-258"

    // Forgelin and ModelLoader
	compile files("lib/modelloader-1.1.7.jar")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


processResources {
    inputs.property "version", mod_version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': mod_version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

task updateModVersion {
    doLast {
        exec {
            workingDir 'src/main/kotlin/com/cout970/magneticraft'
            commandLine 'sed', '-i', '-e', """s/version = "[1-9]*\\.[1-9]*\\.[1-9]*"/version = "${mod_version}"/g""", 'Magneticraft.kt'
        }
    }
}

compileKotlin.dependsOn updateModVersion

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    if(buildNumber == null || buildNumber == ""){
        buildNumber = "dev"
    }
    return buildNumber
}
